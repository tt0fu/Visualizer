#pragma kernel CSMain
#include <UnityCG.cginc>

# define X 1
# define Y 1
# define Z 1
float exp_bins;
float sample_rate;
float lowest_frequency;
uint dft_iteration_count;
StructuredBuffer<float> samples;
uint samples_size;
uint samples_start;
RWStructuredBuffer<float3> dft;

float get_sample(uint index) {
    return samples.Load((index + samples_start) % samples_size);
}

void calculate_dft(int note) {
    float2 amplitude = 0;
    float phase_delta = ((pow(2, note / exp_bins) * lowest_frequency) / sample_rate) * UNITY_TWO_PI;
    float cur_phase = -phase_delta * dft_iteration_count;

    float halfWindowSize = 4.0 / (phase_delta / UNITY_TWO_PI);
    float totalWindow = 0;
    for (uint sample_index = 0; sample_index < dft_iteration_count; sample_index++) {
        float window = max(
            0, (dft_iteration_count - sample_index) * (2 - (dft_iteration_count - sample_index) / (halfWindowSize)));

        float2 sinCos;
        sincos(cur_phase, sinCos.x, sinCos.y);

        amplitude += sinCos * get_sample(samples_size - sample_index - 1) * window;
        totalWindow += window;
        cur_phase += phase_delta;
    }
    dft[note] = float3(length(amplitude) / totalWindow, amplitude);
}

[numthreads(X,Y,Z)]
void CSMain(uint3 id : SV_DispatchThreadID) {
    uint index = id.x + id.y * X + id.z * X * Y;
    calculate_dft(index);
}
